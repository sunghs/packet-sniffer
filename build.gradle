import java.nio.charset.StandardCharsets

buildscript {
    ext {
        name = "packet-sniffer"
        group = "sunghs.packet.sniff"
        version = "1.0.0-SNAPSHOT"
        javaVersion = JavaVersion.VERSION_11
    }
    repositories {
    }
}

plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
    id 'java'
}

sourceCompatibility = "${javaVersion}"
targetCompatibility = "${javaVersion}"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = StandardCharsets.UTF_8.name()
}

dependencies {
    annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')
    annotationProcessor('org.projectlombok:lombok')
    implementation('org.projectlombok:lombok')

    implementation('org.springframework.boot:spring-boot-starter')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('com.querydsl:querydsl-jpa')
    implementation('org.springframework.boot:spring-boot-starter-jdbc')
    implementation('org.springframework.kafka:spring-kafka')
    implementation('org.apache.kafka:kafka-streams')
    implementation('mysql:mysql-connector-java')
    implementation('org.apache.commons:commons-lang3:3.12.0')

    implementation('commons-codec:commons-codec:1.9')
    implementation('com.google.code.gson:gson:2.8.6') {
        changing = true
    }

    implementation('org.pcap4j:pcap4j-core:1.8.2')
    implementation('org.pcap4j:pcap4j-packetfactory-static:1.8.2')

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

def querydslEnv = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    querydslSourcesDir = querydslEnv
}

sourceSets {
    main.java.srcDir querydslEnv
}

configurations {
    querydsl.extendsFrom compileClasspath
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

test {
    useJUnitPlatform()
}
